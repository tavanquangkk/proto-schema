plugins {
    id 'java'
    id 'maven-publish'
    id 'com.google.protobuf' version '0.9.4'
}

// Nên đổi group cho khớp với package trong proto
group = 'com.tavanquangkk.proto' 
version = "1.0.${System.getenv('GITHUB_RUN_NUMBER') ?: '1'}"

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.protobuf:protobuf-java:3.25.1'
}

// QUAN TRỌNG: Chỉ định nơi chứa file proto
sourceSets {
    main {
        proto {
            srcDir 'proto'
        }
        java {
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.25.1"
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {}
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = "proto-java"
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/tavanquangkk/proto-schema")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}